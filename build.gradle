plugins {
  id 'application'
  id 'java'
  id 'org.openjfx.javafxplugin' version '0.0.7'
  id 'org.beryx.jlink' version '2.11.0'
}

repositories {
    mavenCentral()
}

javafx {
    version = "12"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mainClassName = "$moduleName/algat.App"

sourceCompatibility = 11
targetCompatibility = 11


jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'AlgaT'
    }
}

task instrun(type: Exec) {
    dependsOn installDist
    workingDir "$buildDir/install/$project.name/bin"
    commandLine 'sh', '-c', "./$project.name"
}

jar {
    manifest {
        attributes(
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Main-Class'     : 'algat.App'
        )
    }
}

